.sourcefile "EntityListeners.jasm"
.implements org/bukkit/event/Listener
.super java/lang/Object
.class public net/strokkur/explodinglasershootingbees/listeners/EntityListeners {

    .method public <init> ()V {
        code: {
            aload this
            invokespecial java/lang/Object.<init> ()V
            return
        }
    }

    .visible-annotation org/bukkit/event/EventHandler {}
    .method onAddEntity (Lcom/destroystokyo/paper/event/entity/EntityAddToWorldEvent;)V {
        parameters: { this, event },
        code: {
            aload event
            invokevirtual com/destroystokyo/paper/event/entity/EntityAddToWorldEvent.getEntity ()Lorg/bukkit/entity/Entity;
            dup
            instanceof Lorg/bukkit/entity/Bee;
            ifeq RETURN_NO_BEE

            checkcast Lorg/bukkit/entity/Bee;
            astore bee

            // Get the plugin instance
            invokestatic net/strokkur/explodinglasershootingbees/ExplodingLaserShootingBees.getInstance ()Lnet/strokkur/explodinglasershootingbees/ExplodingLaserShootingBees;
            astore plugin

            aload this
            aload bee
            aload plugin
            invokevirtual net/strokkur/explodinglasershootingbees/listeners/EntityListeners.scheduleBeeTask (Lorg/bukkit/entity/Bee;Lorg/bukkit/plugin/java/JavaPlugin;)V

        RETURN:
            return

        RETURN_NO_BEE:
            pop
            return
        }
    }

    .method private scheduleBeeTask (Lorg/bukkit/entity/Bee;Lorg/bukkit/plugin/java/JavaPlugin;)V {
        parameters: { this, bee, plugin },
        code: {
            // Scheduler
            aload bee
            invokeinterface org/bukkit/entity/Bee.getScheduler ()Lio/papermc/paper/threadedregions/scheduler/EntityScheduler;

            // Plugin
            aload plugin

            // Task
            new net/strokkur/explodinglasershootingbees/listeners/BeeRunnable
            dup
            aload bee
            aload plugin
            invokespecial net/strokkur/explodinglasershootingbees/listeners/BeeRunnable.<init> (Lorg/bukkit/entity/Bee;Lorg/bukkit/plugin/java/JavaPlugin;)V

            // Retired runnable
            aconst_null

            // Initial delay
            lconst_1

            // Period ticks
            lconst_1

            line 7
            invokeinterface io/papermc/paper/threadedregions/scheduler/EntityScheduler.runAtFixedRate (Lorg/bukkit/plugin/Plugin;Ljava/util/function/Consumer;Ljava/lang/Runnable;JJ)Lio/papermc/paper/threadedregions/scheduler/ScheduledTask;
            pop

            return
        }
    }
}