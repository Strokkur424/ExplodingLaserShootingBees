/*
 * ExplodingLaserShootingBees  Exploding, laser shooting bees
 * Copyright (C) 2025  Strokkur24
 *
 * This program is free software; you can redistribute it and/or modify
 * it under the terms of the GNU General Public License as published by
 * the Free Software Foundation; either version 2 of the License, or
 * (at your option) any later version.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License along
 * with this program; if not, see <https://www.gnu.org/licenses/>.
*/
.sourcefile "ExplodingLaserShootingBees.jasm"
.super org/bukkit/plugin/java/JavaPlugin
.class public net/strokkur/explodinglasershootingbees/ExplodingLaserShootingBees {

    .method public <init> ()V {
        code: {
            aload this
            invokespecial org/bukkit/plugin/java/JavaPlugin.<init> ()V
            return
        }
    }

    .visible-annotation java/lang/Override {}
    .method public onLoad ()V {
        parameters: { this },
        code: {
            line 0
            aload this
            invokevirtual net/strokkur/explodinglasershootingbees/ExplodingLaserShootingBees.getLogger ()Ljava/util/logging/Logger;
            ldc " "
            invokevirtual java/util/logging/Logger.info (Ljava/lang/String;)V

            aload this
            invokevirtual net/strokkur/explodinglasershootingbees/ExplodingLaserShootingBees.getLogger ()Ljava/util/logging/Logger;
            ldc "  ____  ______ ______  _____ "
            invokevirtual java/util/logging/Logger.info (Ljava/lang/String;)V

            aload this
            invokevirtual net/strokkur/explodinglasershootingbees/ExplodingLaserShootingBees.getLogger ()Ljava/util/logging/Logger;
            ldc " |  _ \\|  ____|  ____|/ ____|"
            invokevirtual java/util/logging/Logger.info (Ljava/lang/String;)V

            aload this
            invokevirtual net/strokkur/explodinglasershootingbees/ExplodingLaserShootingBees.getLogger ()Ljava/util/logging/Logger;
            ldc " | |_) | |__  | |__  | (___  "
            invokevirtual java/util/logging/Logger.info (Ljava/lang/String;)V

            aload this
            invokevirtual net/strokkur/explodinglasershootingbees/ExplodingLaserShootingBees.getLogger ()Ljava/util/logging/Logger;
            ldc " |  _ <|  __| |  __|  \\___ \\ "
            invokevirtual java/util/logging/Logger.info (Ljava/lang/String;)V

            aload this
            invokevirtual net/strokkur/explodinglasershootingbees/ExplodingLaserShootingBees.getLogger ()Ljava/util/logging/Logger;
            ldc " | |_) | |____| |____ ____) |"
            invokevirtual java/util/logging/Logger.info (Ljava/lang/String;)V

            aload this
            invokevirtual net/strokkur/explodinglasershootingbees/ExplodingLaserShootingBees.getLogger ()Ljava/util/logging/Logger;
            ldc " |____/|______|______|_____/ "
            invokevirtual java/util/logging/Logger.info (Ljava/lang/String;)V

            aload this
            invokevirtual net/strokkur/explodinglasershootingbees/ExplodingLaserShootingBees.getLogger ()Ljava/util/logging/Logger;
            ldc " "
            invokevirtual java/util/logging/Logger.info (Ljava/lang/String;)V

            return
        }
    }

    .visible-annotation java/lang/Override {}
    .method public onEnable ()V {
        parameters: { this },
        code: {
            aload this
            invokevirtual net/strokkur/explodinglasershootingbees/ExplodingLaserShootingBees.getServer ()Lorg/bukkit/Server;
            invokeinterface org/bukkit/Server.getPluginManager ()Lorg/bukkit/plugin/PluginManager;

            new net/strokkur/explodinglasershootingbees/listeners/EntityListeners
            dup
            invokespecial net/strokkur/explodinglasershootingbees/listeners/EntityListeners.<init> ()V
            aload this
            invokeinterface org/bukkit/plugin/PluginManager.registerEvents (Lorg/bukkit/event/Listener;Lorg/bukkit/plugin/Plugin;)V

            return
        }
    }

    .method public isExempt (Lorg/bukkit/entity/Player;)Z {
        parameters: { this, player },
        code: {
            aload player
            ldc "explodinglasershootingbees.exempt"
            invokeinterface org/bukkit/entity/Player.hasPermission (Ljava/lang/String;)Z
            ireturn
        }
    }

    .method public static getInstance ()Lnet/strokkur/explodinglasershootingbees/ExplodingLaserShootingBees; {
        code: {
            ldc Lnet/strokkur/explodinglasershootingbees/ExplodingLaserShootingBees;
            invokestatic org/bukkit/plugin/java/JavaPlugin.getPlugin (Ljava/lang/Class;)Lorg/bukkit/plugin/java/JavaPlugin;
            checkcast Lnet/strokkur/explodinglasershootingbees/ExplodingLaserShootingBees;
            areturn
        }
    }
}